method QuickSort(s: seq<int>) returns(r: seq<int>)
  decreases |s|;
  ensures |s| == |r|;
  ensures forall i :: i in s ==> i in r;
  ensures forall i :: i in r ==> i in s;
  ensures multiset(s) == multiset(r);
  ensures forall i,j :: 0 <= i < j < |r| ==> r[i] <= r[j];
{
  if(|s| > 1) {
    var a, b, c := Partition(s);
    a := QuickSort(a);
    c := QuickSort(c);
    r := a + [b] + c;
  }else{
    r := s;
  }
}

method Partition(t: seq<int>) returns (a: seq<int>, b: int, c: seq<int>)
  requires 1 < |t|;
  ensures |t| == |a| + 1 + |c|;
  ensures forall i :: i in t ==> i in a+[b]+c;
  ensures forall i :: i in a+[b]+c ==> i in t;
  ensures multiset(t) == multiset(a+[b]+c);
  ensures forall i :: 0 <= i < |a| ==> a[i] <= b;
  ensures forall i :: 0 <= i < |c| ==> b < c[i];
{
  b := t[0];
  a := [];
  c := [];
  var i: int := 1;
  assert ([t[0]]) == t[..i];
  while(i < |t|)
    invariant 1 <= i <= |t|;
    invariant forall j :: j in a ==> j <= b;
    invariant forall j :: j in c ==> b < j;
    invariant |a+[b]+c| == |t[..i]|;
    invariant forall j :: j in t[..i] ==> j in a+[b]+c;
    invariant forall j :: j in a+[b]+c ==> j in t[..i];
    invariant multiset(a+[b]+c) == multiset(t[..i]);
  {
    if(t[i] <= b){
      a := a + [t[i]];
    }else{
      c := c + [t[i]];
    }
    assert t[..i]+[t[i]] == t[..i+1];
    i := i + 1;
  }
  assert t[..i] == t;
}
